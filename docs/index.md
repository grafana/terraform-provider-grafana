---
layout: ""
page_title: "Provider: Grafana"
description: |-
  The Grafana provider provides configuration management resources for Grafana.
---

# Grafana Provider

The Grafana provider provides configuration management resources for
[Grafana](https://grafana.com/).

## Example Usage

### Creating a Grafana provider

```terraform
provider "grafana" {
  url  = "http://grafana.example.com/"
  auth = var.grafana_auth
}
```

### Creating a Grafana organization provider (on-premise)

```terraform
// Step 1: Create an organization
provider "grafana" {
  alias = "base"
  url   = "http://grafana.example.com/"
  auth  = var.grafana_auth
}

resource "grafana_organization" "my_org" {
  provider = grafana.base
  name     = "my_org"
}

// Step 2: Create resources within the organization
provider "grafana" {
  alias  = "my_org"
  url    = "http://grafana.example.com/"
  auth   = var.grafana_auth
  org_id = grafana_organization.my_org.org_id
}

resource "grafana_folder" "my_folder" {
  provider = grafana.my_org

  title = "Test Folder"
}
```

### Creating a Grafana Cloud stack provider

```terraform
// Step 1: Create a stack
provider "grafana" {
  alias         = "cloud"
  cloud_api_key = "my-token"
}

resource "grafana_cloud_stack" "my_stack" {
  provider = grafana.cloud

  name        = "myteststack"
  slug        = "myteststack"
  region_slug = "us"
}

resource "grafana_api_key" "management" {
  provider = grafana.cloud

  cloud_stack_slug = grafana_cloud_stack.my_stack.slug
  name             = "management-key"
  role             = "Admin"
}

// Step 2: Create resources within the stack
provider "grafana" {
  alias = "my_stack"

  url  = grafana_cloud_stack.my_stack.url
  auth = grafana_api_key.management.key
}

resource "grafana_folder" "my_folder" {
  provider = grafana.my_stack

  title = "Test Folder"
}
```

### Installing Synthetic Monitoring on a new Grafana Cloud Stack

```terraform
// Step 1: Create a stack
provider "grafana" {
  alias         = "cloud"
  cloud_api_key = "<my-api-key>"
  sm_url        = "<synthetic-monitoring-api-url>"
}

resource "grafana_cloud_stack" "sm_stack" {
  provider = grafana.cloud

  name        = "<stack-name>"
  slug        = "<stack-slug>"
  region_slug = "us"
}

// Step 2: Install Synthetic Monitoring on the stack
resource "grafana_cloud_api_key" "metrics_publish" {
  provider = grafana.cloud

  name           = "MetricsPublisherForSM"
  role           = "MetricsPublisher"
  cloud_org_slug = "<org-slug>"
}

resource "grafana_synthetic_monitoring_installation" "sm_stack" {
  provider = grafana.cloud

  stack_id              = grafana_cloud_stack.sm_stack.id
  metrics_instance_id   = grafana_cloud_stack.sm_stack.prometheus_user_id
  logs_instance_id      = grafana_cloud_stack.sm_stack.logs_user_id
  metrics_publisher_key = grafana_cloud_api_key.metrics_publish.key
}

// Step 3: Interact with Synthetic Monitoring
provider "grafana" {
  alias           = "sm"
  sm_access_token = grafana_synthetic_monitoring_installation.sm_stack.sm_access_token
  sm_url          = "<synthetic-monitoring-api-url>"
}

data "grafana_synthetic_monitoring_probes" "main" {
  provider = grafana.sm
  depends_on = [
    grafana_synthetic_monitoring_installation.sm_stack
  ]
}

resource "grafana_synthetic_monitoring_check" "ping" {
  provider = grafana.sm

  job     = "Ping Default"
  target  = "grafana.com"
  enabled = false
  probes = [
    data.grafana_synthetic_monitoring_probes.main.probes.Atlanta,
  ]
  labels = {
    foo = "bar"
  }
  settings {
    ping {}
  }
}
```

### Managing Grafana OnCall

```terraform
// Step 1: Configure provider block.
// Go to the Grafana OnCall in your stack and create api token in the settings tab.It will be your oncall_access_token.
// If you are using Grafana OnCall OSS consider set oncall_url. You can get it in OnCall -> settings -> API URL.
provider "grafana" {
  alias               = "oncall"
  oncall_access_token = "my_oncall_token"
}

data "grafana_oncall_user" "alex" {
  username = "alex"
}

// Step 2: Interact with Grafana OnCall
resource "grafana_oncall_integration" "test-acc-integration" {
  provider = grafana.oncall
  name     = "my integration"
  type     = "grafana"
  default_route {
    escalation_chain_id = grafana_oncall_escalation_chain.default.id
  }
}

resource "grafana_oncall_escalation_chain" "default" {
  provider = grafana.oncall
  name     = "default"
}

resource "grafana_oncall_escalation" "example_notify_step" {
  escalation_chain_id = grafana_oncall_escalation_chain.default.id
  type                = "notify_persons"
  persons_to_notify = [
    data.grafana_oncall_user.alex.id
  ]
  position = 0
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `auth` (String, Sensitive) API token or basic auth `username:password`. May alternatively be set via the `GRAFANA_AUTH` environment variable.
- `ca_cert` (String) Certificate CA bundle to use to verify the Grafana server's certificate. May alternatively be set via the `GRAFANA_CA_CERT` environment variable.
- `cloud_api_key` (String, Sensitive) API key for Grafana Cloud. May alternatively be set via the `GRAFANA_CLOUD_API_KEY` environment variable.
- `cloud_api_url` (String) Grafana Cloud's API URL. May alternatively be set via the `GRAFANA_CLOUD_API_URL` environment variable.
- `http_headers` (Map of String, Sensitive) Optional. HTTP headers mapping keys to values used for accessing the Grafana and Grafana Cloud APIs. May alternatively be set via the `GRAFANA_HTTP_HEADERS` environment variable in JSON format.
- `insecure_skip_verify` (Boolean) Skip TLS certificate verification. May alternatively be set via the `GRAFANA_INSECURE_SKIP_VERIFY` environment variable.
- `oncall_access_token` (String, Sensitive) A Grafana OnCall access token. May alternatively be set via the `GRAFANA_ONCALL_ACCESS_TOKEN` environment variable.
- `oncall_url` (String) An Grafana OnCall backend address. May alternatively be set via the `GRAFANA_ONCALL_URL` environment variable.
- `org_id` (Number) The default organization id to operate on within grafana. For resources that have an `org_id` attribute, the resource-level attribute has priority. May alternatively be set via the `GRAFANA_ORG_ID` environment variable.
- `retries` (Number) The amount of retries to use for Grafana API and Grafana Cloud API calls. May alternatively be set via the `GRAFANA_RETRIES` environment variable.
- `sm_access_token` (String, Sensitive) A Synthetic Monitoring access token. May alternatively be set via the `GRAFANA_SM_ACCESS_TOKEN` environment variable.
- `sm_url` (String) Synthetic monitoring backend address. May alternatively be set via the `GRAFANA_SM_URL` environment variable. The correct value for each service region is cited in the [Synthetic Monitoring documentation](https://grafana.com/docs/grafana-cloud/synthetic-monitoring/private-probes/#probe-api-server-url). Note the `sm_url` value is optional, but it must correspond with the value specified as the `region_slug` in the `grafana_cloud_stack` resource. Also note that when a Terraform configuration contains multiple provider instances managing SM resources associated with the same Grafana stack, specifying an explicit `sm_url` set to the same value for each provider ensures all providers interact with the same SM API.
- `store_dashboard_sha256` (Boolean) Set to true if you want to save only the sha256sum instead of complete dashboard model JSON in the tfstate.
- `tls_cert` (String) Client TLS certificate file to use to authenticate to the Grafana server. May alternatively be set via the `GRAFANA_TLS_CERT` environment variable.
- `tls_key` (String) Client TLS key file to use to authenticate to the Grafana server. May alternatively be set via the `GRAFANA_TLS_KEY` environment variable.
- `url` (String) The root URL of a Grafana server. May alternatively be set via the `GRAFANA_URL` environment variable.

## Authentication

One, or many, of the following authentication settings must be set. Each authentication setting allows a subset of resources to be used

### `auth`

This can be a Grafana API key, basic auth `username:password`, or a
[Grafana API key](https://grafana.com/docs/grafana/latest/developers/http_api/create-api-tokens-for-org/).

### `cloud_api_key`

An API key created on the [Grafana Cloud Portal](https://grafana.com/docs/grafana-cloud/reference/create-api-key/).

### `sm_access_token`

[Synthetic Monitoring](https://grafana.com/docs/grafana-cloud/synthetic-monitoring/)
endpoints require a dedicated access token. You may obtain an access token with its
[Registration API](https://github.com/grafana/synthetic-monitoring-api-go-client/blob/main/docs/API.md#registration-api).

```console
curl \
  -X POST \
  -H 'Content-type: application/json; charset=utf-8' \
  -H "Authorization: Bearer $GRAFANA_CLOUD_API_KEY" \
  -d '{"stackId": <stack-id>, "metricsInstanceId": <metrics-instance-id>, "logsInstanceId": <logs-instance-id>}' \
  "$SM_API_URL/api/v1/register/install"
```

`GRAFANA_CLOUD_API_KEY` is an API key created on the
[Grafana Cloud Portal](https://grafana.com/docs/grafana-cloud/reference/create-api-key/).
It must have the `MetricsPublisher` role.

`SM_API_URL` is the URL of the Synthetic Monitoring API.
Based on the region of your Grafana Cloud stack, you need to use a different API URL.

Please [see API docs](https://github.com/grafana/synthetic-monitoring-api-go-client/blob/main/docs/API.md#api-url) to find `SM_API_URL` for your region.

`stackId`, `metricsInstanceId`, and `logsInstanceId` may also be obtained on
the portal. First, you need to create a Stack by clicking "Add Stack". When it's
created you will be taken to its landing page on the portal. Get your `stackId`
from the URL in your browser:

```
https://grafana.com/orgs/<org-slug>/stacks/<stack-id>
```

Next, go to "Details" for Prometheus. Again, get `metricsInstanceId` from your URL:

```
https://grafana.com/orgs/<org-slug>/hosted-metrics/<metrics-instance-id>
```

Finally, go back to your stack page, and go to "Details" for Loki to get
`logsInstanceId`.

```
https://grafana.com/orgs/<org-slug>/hosted-logs/<logs-instance-id>
```

### `oncall_access_token`

[Grafana OnCall](https://grafana.com/docs/grafana-cloud/oncall/oncall-api-reference/)
uses API keys to allow access to the API. You can request a new OnCall API key in OnCall -> Settings page.
